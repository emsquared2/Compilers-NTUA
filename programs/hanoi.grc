fun solve () : nothing

   var moves : int;

   fun hanoi (ref moves : int; rings : int; ref source, target, auxiliary : char[]) : nothing

      fun move (ref source, target : char[]) : nothing
      {
         writeString("Move from ");
         writeString(source);
         writeString(" to ");
         writeString(target);
         writeString(".\n");
      }

   { $ hanoi
      if rings >= 1 then {
         moves <- moves + 1;
         hanoi(moves, rings-1, source, auxiliary, target);
         move(source, target);
         hanoi(moves, rings-1, auxiliary, target, source);
      }
   } $ hanoi

   var NumberOfRings : int;

{ $ solve
   moves <- 0;
   writeString("Please, give me the number of rings: ");
   NumberOfRings <- readInteger();
   writeString("\nHere is the solution:\n\n");
   hanoi(moves, NumberOfRings, "left", "right", "middle");
   writeString("\nProblem solved in ");
   writeInteger(moves);
   writeString(" moves.\n");
} $ solve